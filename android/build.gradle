// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        maven { url "https://maven.aliyun.com/repository/google" }
        maven { url "https://maven.aliyun.com/repository/central" }
        maven { url "https://maven.aliyun.com/repository/jcenter" }
        maven { url "https://maven.aliyun.com/repository/public" }
        maven {
            url "http://download.flutter.io"
            allowInsecureProtocol = true
        }
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.4.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.23"
    }
}

plugins {
    // Kotlin JVM plugin for using toolchain
    id 'org.jetbrains.kotlin.jvm' version '1.9.23' apply false
}

ext {
    kotlin_version = '1.9.23'
}

allprojects {
    repositories {
        maven { url "https://maven.aliyun.com/repository/google" }
        maven { url "https://maven.aliyun.com/repository/central" }
        maven { url "https://maven.aliyun.com/repository/jcenter" }
        maven { url "https://maven.aliyun.com/repository/public" }
        maven {
            url "http://download.flutter.io"
            allowInsecureProtocol = true
        }
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'

subprojects {
    // 在所有 Flutter 插件子项目里修复 manifest、namespace
    afterEvaluate { project ->
        if (project.extensions.findByName("android") != null) {
            Integer pluginCompileSdk = project.android.compileSdk
            if (pluginCompileSdk != null && pluginCompileSdk < 31) {
                project.logger.error(
                    "Warning: Overriding compileSdk version in Flutter plugin: "
                        + project.name
                        + " from "
                        + pluginCompileSdk
                        + " to 31 (to work around https://issuetracker.google.com/issues/199180389)."
                )
                project.android {
                    compileSdk 31
                }
            }
        }
        if (project.hasProperty('android')) {
            project.android {
                if (namespace == null) {
                    project.logger.error(
                        "Warning: 'namespace' is not specified in Flutter plugin: "
                            + project.name
                            + ". Defaulting to the package name "
                            + project.group
                    )
                    namespace project.group
                }
            }

            // Task to fix manifests and namespace
            project.tasks.register("fixManifestsAndNamespace") {
                doLast {
                    // Ensure namespace in build.gradle
                    def buildGradleFile = file("${project.projectDir}/build.gradle")
                    if (buildGradleFile.exists()) {
                        def buildGradleContent = buildGradleFile.getText('UTF-8')
                        def manifestFile = file("${project.projectDir}/src/main/AndroidManifest.xml")
                        if (manifestFile.exists()) {
                            def manifestContent = manifestFile.getText('UTF-8')
                            def packageName = manifestContent.find(/package="([^"]+)"/) { m,p -> p }
                            if (packageName && !buildGradleContent.contains("namespace")) {
                                println "Setting namespace in ${buildGradleFile}"
                                buildGradleContent = buildGradleContent.replaceFirst(
                                    /android\s*\{/, "android {\n    namespace '${packageName}'"
                                )
                                buildGradleFile.write(buildGradleContent, 'UTF-8')
                            }
                        }
                    }
                    // Remove package attribute from all manifests
                    def manifests = fileTree(dir: project.projectDir, includes: ['**/AndroidManifest.xml'])
                    manifests.each { File mf ->
                        def content = mf.getText('UTF-8')
                        if (content.contains('package=')) {
                            println "Removing package attribute from ${mf}"
                            content = content.replaceAll(/package="[^"]*"/, '')
                            mf.write(content, 'UTF-8')
                        }
                    }
                }
            }

            // Make preBuild depend on fixManifestsAndNamespace
            project.tasks.matching { it.name.startsWith("preBuild") }.all {
                dependsOn project.tasks.named("fixManifestsAndNamespace")
            }
        }
    }

    // 将每个子项目的输出放到根 build 目录下
    project.buildDir = "${rootProject.buildDir}/${project.name}"
    project.evaluationDependsOn(':app')
}

// 全局 clean 任务
tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
